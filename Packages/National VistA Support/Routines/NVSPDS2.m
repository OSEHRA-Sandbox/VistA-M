NVSPDS2 ;emciss/maw-scramble patient data (cont) ; 09/01/02
 ;;6.0;EMC TEST ACCOUNT RESET UTILITIES; 01 Jun 1999;;Build 24
 ;
 N NVSDATA
 ;
 ; note:  the variable NVSDFN is set in the main loop through ^DPT(...)
 ;        that begins in routine ^NVSPDS.
 I '$D(NVSDFN) Q
 ;
 ; scramble primary next of kin data...
 S NVSDATA=$G(^DPT(NVSDFN,.21))
 I NVSDATA'="" D
 .I $P(NVSDATA,U,1)'="" D PFEDIT^NVSPDSU(NVSDFN,".211",$$REVN^NVSPDSU($P(NVSDATA,U,1)))
 .I $P(NVSDATA,U,6)'="" D PFEDIT^NVSPDSU(NVSDFN,".216",$$CITY^NVSPDSU)
 .I $P(NVSDATA,U,7)'="" D PFEDIT^NVSPDSU(NVSDFN,".217",$P($$ST^NVSPDSU(),U,2))
 .I $P(NVSDATA,U,8)'="" D PFEDIT^NVSPDSU(NVSDFN,".218",$$SCR^NVSPDSU($P(NVSDATA,U,8)))
 .I $P(NVSDATA,U,9)'="" D PFEDIT^NVSPDSU(NVSDFN,".219",$$SCR^NVSPDSU($P(NVSDATA,U,9)))
 K NVSDATA
 ;
 ; scramble secondary next of kin data...
 S NVSDATA=$G(^DPT(NVSDFN,.211))
 I NVSDATA'="" D
 .I $P(NVSDATA,U,1)'="" D PFEDIT^NVSPDSU(NVSDFN,".2191",$$REVN^NVSPDSU($P(NVSDATA,U,1)))
 .I $P(NVSDATA,U,6)'="" D PFEDIT^NVSPDSU(NVSDFN,".2196",$$CITY^NVSPDSU)
 .I $P(NVSDATA,U,7)'="" D PFEDIT^NVSPDSU(NVSDFN,".2197",$P($$ST^NVSPDSU(),U,2))
 .I $P(NVSDATA,U,8)'="" D PFEDIT^NVSPDSU(NVSDFN,".2198",$$SCR^NVSPDSU($P(NVSDATA,U,8)))
 .I $P(NVSDATA,U,9)'="" D PFEDIT^NVSPDSU(NVSDFN,".2199",$$SCR^NVSPDSU($P(NVSDATA,U,9)))
 K NVSDATA
 ;
 ; hash up any zip+4 entries...
 S NVSDATA=$G(^DPT(NVSDFN,.22))
 I NVSDATA'="" D
 .F NVSX=1:1:7 D
 ..I $P(NVSDATA,U,NVSX)'="" D PFEDIT^NVSPDSU(NVSDFN,".220"_NVSX,$$SCR^NVSPDSU($P(NVSDATA,U,NVSX)))
 .K NVSX
 K NVSDATA
 ;
 ; scramble spouse employer data...
 S NVSDATA=$G(^DPT(NVSDFN,.25))
 I NVSDATA'="" D
 .I $P(NVSDATA,U,1)'="" D
 ..S NVSEMPN=$P("AB DICK^IBM^EATS CAFE^PENNY'S^IRON WORKS INC.^TRUCKING INC.^EAST MED. CENTER^CATERPILLAR^JOHN JONES & SON^SOUTH BANK^FUNPARK INC.^INTERWEST FARMERS^EPX CHEMICAL",U,$R(12)+1)
 ..D PFEDIT^NVSPDSU(NVSDFN,".251",NVSEMPN)
 ..K NVSEMPN
 .I $P(NVSDATA,U,5)'="" D PFEDIT^NVSPDSU(NVSDFN,".255",$$CITY^NVSPDSU)
 .I $P(NVSDATA,U,6)'="" D PFEDIT^NVSPDSU(NVSDFN,".256",$P($$ST^NVSPDSU(),U,2))
 .I $P(NVSDATA,U,7)'="" D PFEDIT^NVSPDSU(NVSDFN,".257",$$SCR^NVSPDSU($P(NVSDATA,U,7)))
 .I $P(NVSDATA,U,8)'="" D PFEDIT^NVSPDSU(NVSDFN,".258",$$SCR^NVSPDSU($P(NVSDATA,U,8)))
 K NVSDATA
 ;
 ; scramble guardian data...
 S NVSDATA=$G(^DPT(NVSDFN,.29))
 I NVSDATA'="" D
 .I $P(NVSDATA,U,4)'="" D PFEDIT^NVSPDSU(NVSDFN,".2912",$$REVN^NVSPDSU($P(NVSDATA,U,1)))
 .I $P(NVSDATA,U,8)'="" D PFEDIT^NVSPDSU(NVSDFN,".2916",$$CITY^NVSPDSU)
 .I $P(NVSDATA,U,9)'="" D PFEDIT^NVSPDSU(NVSDFN,".2917",$P($$ST^NVSPDSU(),U,2))
 .I $P(NVSDATA,U,10)'="" D PFEDIT^NVSPDSU(NVSDFN,".2918",$$SCR^NVSPDSU($P(NVSDATA,U,8)))
 .I $P(NVSDATA,U,11)'="" D PFEDIT^NVSPDSU(NVSDFN,".2919",$$SCR^NVSPDSU($P(NVSDATA,U,9)))
 K NVSDATA
 ;
 ; scramble claim number...
 I $P($G(^DPT(NVSDFN,.31)),U,3)'="" S $P(^DPT(NVSDFN,.31),U,3)="SS"
 ;
 ; scramble employer data...
 S NVSDATA=$G(^DPT(NVSDFN,.311))
 I NVSDATA'="" D
 .I $P(NVSDATA,U,1)'="" D
 ..S NVSEMPN=$P("DELTA LINES^LEWIS & SON INC.^A&B AUTO^GENERAL MOTORS^ACME INSURANCE^SEARS^SECOND BANK^POP'S GROCERY^BOX CO. INC.^PAPER CO.",U,$R(9)+1)
 ..D PFEDIT^NVSPDSU(NVSDFN,".3111",NVSEMPN)
 ..K NVSEMPN
 .I $P(NVSDATA,U,6)'="" D PFEDIT^NVSPDSU(NVSDFN,".3116",$$CITY^NVSPDSU)
 .I $P(NVSDATA,U,7)'="" D PFEDIT^NVSPDSU(NVSDFN,".3117",$P($$ST^NVSPDSU(),U,2))
 .I $P(NVSDATA,U,8)'="" D PFEDIT^NVSPDSU(NVSDFN,".3118",$$SCR^NVSPDSU($P(NVSDATA,U,7)))
 .I $P(NVSDATA,U,9)'="" D PFEDIT^NVSPDSU(NVSDFN,".3119",$$SCR^NVSPDSU($P(NVSDATA,U,8)))
 K NVSDATA
 ;
 ; scramble insurance data...
 S NVSX=0
 F  S NVSX=$O(^DPT(NVSDFN,.312,NVSX)) Q:'NVSX  D
 .S NVSDATA=$G(^DPT(NVSDFN,.312,NVSX,0))
 .I $P(NVSDATA,U,2)="" Q
 .S $P(NVSDATA,U,2)=$$SCR^NVSPDSU($P(NVSDATA,U,2))
 .I $P(NVSDATA,U,9)'="" S $P(NVSDATA,U,9)=$$REVN^NVSPDSU($P(NVSDATA,U,9))
 .I $P(NVSDATA,U,10)'="" S $P(NVSDATA,U,10)=$$SCR^NVSPDSU($P(NVSDATA,U,10))
 .I $P(NVSDATA,U,11)'="" S $P(NVSDATA,U,11)="1234 AGENTS STREET"
 .I $P(NVSDATA,U,12)'="" S $P(NVSDATA,U,12)=$$CITY^NVSPDSU
 .I $P(NVSDATA,U,13)'="" S $P(NVSDATA,U,13)=$P($$ST^NVSPDSU(),U,2)
 .I $P(NVSDATA,U,14)'="" S $P(NVSDATA,U,14)=$$SCR^NVSPDSU($P(NVSDATA,U,14))
 .I $P(NVSDATA,U,15)'="" S $P(NVSDATA,U,15)=$$SCR^NVSPDSU($P(NVSDATA,U,15))
 .I $P(NVSDATA,U,17)'="" S $P(NVSDATA,U,17)=$P(^DPT(NVSDFN,0),U)
 .S ^DPT(NVSDFN,.312,NVSX,0)=NVSDATA
 .K NVSDATA
 .S NVSDATA=$G(^DPT(NVSDFN,.312,NVSX,2))
 .I $P(NVSDATA,U,2)'="" S $P(NVSDATA,U,2)="1234 EMPLOYER STREET"
 .I $P(NVSDATA,U,3)'="" S $P(NVSDATA,U,3)=""
 .I $P(NVSDATA,U,4)'="" S $P(NVSDATA,U,4)=""
 .I $P(NVSDATA,U,5)'="" S $P(NVSDATA,U,5)=$$CITY^NVSPDSU
 .I $P(NVSDATA,U,6)'="" S $P(NVSDATA,U,6)=$P($$ST^NVSPDSU(),U,2)
 .I $P(NVSDATA,U,7)'="" S $P(NVSDATA,U,7)=$$ZIP^NVSPDSU($P(NVSDATA,U,7))
 .I $P(NVSDATA,U,8)'="" S $P(NVSDATA,U,8)=$$SCR^NVSPDSU($P(NVSDATA,U,8))
 .I $P(NVSDATA,U,9)'="" S $P(NVSDATA,U,9)=$$REVN^NVSPDSU($P(NVSDATA,U,9))
 .S ^DPT(NVSDFN,.312,NVSX,2)=NVSDATA
 .K NVSDATA
 .S NVSDATA=$G(^DPT(NVSDFN,.312,NVSX,3))
 .I $P(NVSDATA,U,5)'="" S $P(NVSDATA,U,5)=$$SCR^NVSPDSU($P(NVSDATA,U,5))
 .I $P(NVSDATA,U,6)'="" S $P(NVSDATA,U,6)="1234 INSURED STREET"
 .I $P(NVSDATA,U,7)'="" S $P(NVSDATA,U,7)=""
 .I $P(NVSDATA,U,8)'="" S $P(NVSDATA,U,8)=$$CITY^NVSPDSU
 .I $P(NVSDATA,U,9)'="" S $P(NVSDATA,U,9)=$P($$ST^NVSPDSU(),U,2)
 .I $P(NVSDATA,U,10)'="" S $P(NVSDATA,U,10)=$$SCR^NVSPDSU($P(NVSDATA,U,10))
 .I $P(NVSDATA,U,11)'="" S $P(NVSDATA,U,11)=$$ZIP^NVSPDSU($P(NVSDATA,U,11))
 .S ^DPT(NVSDFN,.312,NVSX,3)=NVSDATA
 .K NVSDATA
 K NVSX
 ;
 ; scramble service-related data...
 S NVSDATA=$G(^DPT(NVSDFN,.32))
 I NVSDATA'="" D
 .I $P(NVSDATA,U,8)'="" S $P(NVSDATA,U,8)=$$SCR^NVSPDSU($P(NVSDATA,U,8))
 .I $P(NVSDATA,U,13)'="" S $P(NVSDATA,U,13)=$$SCR^NVSPDSU($P(NVSDATA,U,13))
 .I $P(NVSDATA,U,18)'="" S $P(NVSDATA,U,18)=$$SCR^NVSPDSU($P(NVSDATA,U,18))
 .S ^DPT(NVSDFN,.32)=NVSDATA
 K NVSDATA
 Q
