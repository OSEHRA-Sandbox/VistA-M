VFDPXIM3 ;DSS/PDW/LM - GENERATE IMMUNIZATION MESSAGE ; 18 April 2011
 ;;2011.1.2;DSS,INC VXVISTA OPEN SOURCE;;11 Jun 2013
 ;Copyright 1995-2013,Document Storage Systems Inc. All Rights Reserved
 ;DSSBUILD - VFDHHL HL7 EXCHANGE 2011.1 * 04/26/11 * vxdev64v2k8_DEVXOS * T8
 ;
 ;LM - Re-namespace to VFDPXIM
 ;
 Q
VIMRPC(VFDRSLT,VFDVIMDA) ;[Remote procedure] - VFD IMMUNIZATION DEMOG
 ; Immplementing code copied from routine ^VFDHHLOT
 S VFDRSLT=""
 ;return demographics of a V IMMUNIZATION entry
 D CONT(.VFDRSLT,VFDVIMDA)
 Q
CONT(VFDRSLT,VFDVIMDA) ; Continuation of VIMRPC
 ; Copied from routine ^VFDZARAVVXU2
 N VFDRPC
 K VFDRPC S VFDRPC(0)=""
 D VIMREV(VFDVIMDA)
 S TMP=$NA(^XTMP("VFDVXA",$J)) K @TMP
 M @TMP=VFDRPC
 K VFDRSLT S VFDRSLT=TMP
 Q
VIMREV(VFDVIMDA) ; IMMUNIZATIONS
 ; Copied from routine ^VFDZARAVVXU2
 N VFDVALS,VFDMSG,XX,LINE,VFDFLDS,VFDMSG,VFDVFDDA,VFDTAR
 S X="",XX=": V IMMUNIZATION file entry :"
 K LINE S $P(LINE,"=",$L(XX)+1)=""
 F Y=X,X,LINE,XX,LINE,X D STUFFRPC(Y)
 ;K DIC,DA,DR S DIC="^AUPNVIMM(",DA=VFDVIMDA D EN^DIQ
 S VFDFLDS=".01;.02;.03;1201;21600.01;21600.02;21600.03"
 S VFDFILE=9000010.11
 D GETS^DIQ(VFDFILE,VFDVIMDA,VFDFLDS,,"VFDTAR","VFDMSG")
 K VFDVALS
 M VFDVALS=VFDTAR(VFDFILE,VFDVIMDA_",") ;VFDVALS(.01)="HEPATITIS B"
 D STUFFLDS(VFDFILE,VFDFLDS,.VFDVALS)
 S X="",XX=": VFD IMMUNIZATION file entry :"
 K LINE S $P(LINE,"=",$L(XX)+1)=""
 F Y=X,X,LINE,XX,LINE,X D STUFFRPC(Y)
 S VFDVFDDA=$$GET1^DIQ(9000010.11,VFDVIMDA,21600.03,"I")
 ;DSS/LM - Avert M-Error by stopping here if no pointer to VFD IMMUNIZATION file
 I '(VFDVFDDA>0) D STUFFRPC("[No corresponding manufacturer data on file.]") Q
 ;DSS/LM - End modification
 K VFDVALS,VFDMSG,VFDTAR
 S VFDFLDS=".01;.02;.03;.04;3;3.1;4",VFDFILE=21630.01
 D GETS^DIQ(VFDFILE,VFDVFDDA,VFDFLDS,,"VFDTAR","VFDMSG")
 K VFDVALS
 M VFDVALS=VFDTAR(VFDFILE,VFDVFDDA_",") ;VFDVALS(.01)="HEPATITIS B CORE ANTIBODY"
 D STUFFLDS(VFDFILE,VFDFLDS,.VFDVALS)
 Q
STUFFRPC(VFDSTR) ;"VFDRPC" target needs to be preset
 N VFDLNLN
 S VFDLN=$O(VFDRPC(""),-1)+1
 S VFDRPC(VFDLN)="    "_VFDSTR
 Q
STUFFLDS(VFDFILE,VFDFLDS,VFDVALS) ; STUFFRPC
 N VFDFLD,I,VFDSTR
 F I=1:1 S VFDFLD=$P(VFDFLDS,";",I) Q:VFDFLD'>0  D SETSTR
 Q
SETSTR ;caled by STUFFLDS(VFDFILE,VFDFLDS,VFDTAR)
 ;given file, field(s)(.01)=, load field label(s) and value int VFDRPC
 N VFDFLDD,VFDSTR,X
 D FIELD^DID(VFDFILE,VFDFLD,"","LABEL","VFDFLDD","VFDMSG")
 S VFDSTR=VFDFLDD("LABEL")_":"
 S X=VFDVALS(VFDFLD)
 S VFDSTR=$$SETSTR^VALM1(X,VFDSTR,25,$L(X))
 D STUFFRPC(VFDSTR)
 Q
