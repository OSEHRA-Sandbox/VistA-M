VOP6P ; OSE/SMH - Install Plan 6 changes into VistA ; 5/13/19 2:52pm
 ;;1.0;PLAN VI;;
 ;
ENV  ; [KIDS Environment Check]
 ; ZEXCEPT: XPDQUIT
 I ^%ZOSF("OS")'["GT.M" D  QUIT
 . D MES^XPDUTL("GT.M/YDB only supported right now")
 . S XPDQUIT=1
 I $TR($$VERSION^%ZOSV(),"-")<6.3001 D  QUIT
 . D MES^XPDUTL("GT.M min version is v6.3-001")
 . S XPDQUIT=1
 QUIT
 ;
TRAN ; [KIDS Transport]
 ; Send new data type in XPDGREF
 M @XPDGREF@(.81,10001)=^DI(.81,10001)
 ;
 ; Send Dialogs that don't install due to input transform
 M @XPDGREF@(.84,1012400.0028)=^DI(.84,1012400.0028)
 M @XPDGREF@(.84,1012400.0029)=^DI(.84,1012400.0029)
 M @XPDGREF@(.84,1012400.003)=^DI(.84,1012400.003)
 M @XPDGREF@(.84,1012400.0031)=^DI(.84,1012400.0031)
 M @XPDGREF@(.84,1012400.0032)=^DI(.84,1012400.0032)
 M @XPDGREF@(.84,1012400.0033)=^DI(.84,1012400.0033)
 M @XPDGREF@(.84,1012400.0034)=^DI(.84,1012400.0034)
 M @XPDGREF@(.84,1012400.0035)=^DI(.84,1012400.0035)
 ;
 ; Send Coversheet Headers pointing to the dialog file
 F DA=0:0 S DA=$O(^ORD(101.24,DA)) Q:'DA  I $P(^ORD(101.24,DA,0),U,8)="C" S @XPDGREF@(101.24,DA)=$P(^ORD(101.24,DA,2),U,3)
 QUIT
 ;
PRE ; [KIDS Pre install]
 N DA,DIK
 ; Install new data type
 S DA=10001,DIK="^DI(.81," D ^DIK
 M ^DI(.81,10001)=@XPDGREF@(.81,10001)
 S DA=10001,DIK="^DI(.81," D IX1^DIK
 ;
 ; Merge Dialogs that can't be installed automatically due to Dialog Input Transform
 S DIK="^DI(.84,"
 F DA=1012400.0028,1012400.0029,1012400.003,1012400.0031,1012400.0032,1012400.0033,1012400.0034,1012400.0035 D ^DIK
 M ^DI(.84,1012400.0028)=@XPDGREF@(.84,1012400.0028)
 M ^DI(.84,1012400.0029)=@XPDGREF@(.84,1012400.0029)
 M ^DI(.84,1012400.003)=@XPDGREF@(.84,1012400.003)
 M ^DI(.84,1012400.0031)=@XPDGREF@(.84,1012400.0031)
 M ^DI(.84,1012400.0032)=@XPDGREF@(.84,1012400.0032)
 M ^DI(.84,1012400.0033)=@XPDGREF@(.84,1012400.0033)
 M ^DI(.84,1012400.0034)=@XPDGREF@(.84,1012400.0034)
 M ^DI(.84,1012400.0035)=@XPDGREF@(.84,1012400.0035)
 F DA=1012400.0028,1012400.0029,1012400.003,1012400.0031,1012400.0032,1012400.0033,1012400.0034,1012400.0035 D IX1^DIK
 ;
 QUIT
 ;
POST ; [KIDS Post install]
 D %
 D KLANGDT
 ;
 ; Update the headers for the Coversheet
 N FDA,DIERR,DA
 F DA=0:0 S DA=$O(^ORD(101.24,DA)) Q:'DA  I $P(^ORD(101.24,DA,0),U,8)="C" S FDA(101.24,DA_",",.23)=@XPDGREF@(101.24,DA)
 D FILE^DIE(,"FDA")
 I $G(DIERR) D MSG^DIALOG()
 QUIT
 ;
% ; % Routines rename
 D MES^XPDUTL("ZOSVGUX -> %ZOSV")
 D RENAME("ZOSVGUX","_ZOSV")
 D MES^XPDUTL("ZISTCPS -> %ZISTCPS")
 D RENAME("ZISTCPS","_ZISTCPS")
 D MES^XPDUTL("DIDT -> %DT")
 D RENAME("DIDT","_DT")
 QUIT
 ;
KLANGDT ; Korean Date Utilities in the Language file
 N KLANG S KLANG=$$FIND1^DIC(.85,,"QX","KOREAN","B")
 I 'KLANG S $EC=",U-KOREAN-NOT-FOUND,"
 N FDA,DIERR
 S FDA(.85,KLANG_",",20.2)="S:$G(%DT)'[""I"" %DT=$G(%DT)_""I"" G CONT^%DT"
 S FDA(.85,KLANG_",",10.2)="S Y=$$FMTE^UKOUTL(Y,$G(%F))"
 S FDA(.85,KLANG_",",10.21)="S Y=$$FMTE^UKOUTL(Y,$G(%F))"
 D FILE^DIE(,"FDA")
 I $D(DIERR) S $EC=",U-FDA-FILE-FAILED,"
 QUIT
 ;
RENAME(FROM,TO) ;Do the rename
 N DIF,XCNP,X,DIE,XCN
 K ^TMP($J)
 S DIF="^TMP($J,",XCNP=0,X=FROM X ^%ZOSF("LOAD")
 S DIE="^TMP($J,",XCN=0,X=TO X ^%ZOSF("SAVE")
 K ^TMP($J)
 Q
