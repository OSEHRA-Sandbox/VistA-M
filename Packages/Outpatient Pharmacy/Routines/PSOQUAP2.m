PSOQUAP2 ;HINES/RMS - UNIFIED PROFILE BASED ON PORTLAND IDEA ; 30 Nov 2007  7:58 AM
 ;;7.0;OUTPATIENT PHARMACY;**294**;DEC 1997;Build 13
 ;
 ;Reference to COVER^ORWPS supported by DBIA 4954
 ;Reference to BCMALG^PSJUTL2 supported by DBIA 5057
EN ;ENTRY POINT FOR HEALTH SUMMARY
 N RPC,RPCT,ALPHA,PSNUM,DRUGNM,RPCNODE,ORDER,SAVE
 D COVER^ORWPS(.RPC,DFN)
 S RPCT=0 F  S RPCT=$O(RPC(RPCT)) Q:'+RPCT  D  ;
 . S RPCNODE=RPC(RPCT)
 . S PSNUM=$P(RPCNODE,"^")
 . S DRUGNM=$$UP^XLFSTR($P(RPCNODE,"^",2))
 . S ORDER=+$P(RPCNODE,"^",3)
 . Q:DRUGNM']""!(ORDER=0)!(PSNUM']"")
 . K SAVE(DRUGNM) S SAVE(DRUGNM,ORDER,PSNUM)=""
 . Q:"ACTIVE^ACTIVE/SUSP^HOLD"'[$P(RPCNODE,"^",4)
 . S ALPHA(DRUGNM,ORDER,PSNUM)=""
 D ADDREM
 D HEADER
 D OUTPUT
 D FOOTER
 Q
HEADER N ATEST,ADATE,AVALUE,ATEXT
 D NVADT^PSOQCF04(DFN,.ATEST,.ADATE,.AVALUE,.ATEXT)
 D CKP^GMTSUP Q:$D(GMTSQIT)
 ;DSS/SMP - BEGIN MODS - Deveteranize
 N VFD S VFD=$G(^%ZOSF("ZVX"))["VX"
 I VFD W $$REPEAT^XLFSTR("-",IOM),!,"Alphabetized list of outpatient Rx's, inpatient orders, remote and Meds/OTC",!,"from Elsewhere"
 E  W $$REPEAT^XLFSTR("-",IOM),!,"Alphabetized list of outpatient Rx's, inpatient orders, remote and Non-VA meds"
 D CKP^GMTSUP Q:$D(GMTSQIT)
 I VFD W !,"Legend: OPT = outpatient prescription, INP = inpatient order"
 E  W !,"Legend: OPT = VA issued outpatient prescription, INP = VA issued inpatient order"
 D CKP^GMTSUP Q:$D(GMTSQIT)
 I VFD W !,"Meds/OTC from Elsewhere Last Documented On: "
 E  W !,"Non-VA Meds Last Documented On: "
 ;DSS/SMP - END MODS
 W $S(+ADATE:$$FMTE^XLFDT(ADATE,"D"),1:"** Data not found **")
 D CKP^GMTSUP Q:$D(GMTSQIT)
 W !,$$REPEAT^XLFSTR("-",IOM)
 D CKP^GMTSUP Q:$D(GMTSQIT)
 Q
OUTPUT N DRUGNM,ORDER,PSNUM
 N PACK,PACKREF,SIGLINE,ORDNUM
 N LASTACT,OTLINE
 S DRUGNM="" F  S DRUGNM=$O(ALPHA(DRUGNM)) Q:DRUGNM']""  D  K SAVE(DRUGNM)  ;
 . S ORDER="" F  S ORDER=$O(ALPHA(DRUGNM,ORDER)) Q:ORDER']""  D  ;
 .. S PSNUM="" F  S PSNUM=$O(ALPHA(DRUGNM,ORDER,PSNUM)) Q:PSNUM']""  D  ;
 ... S PACK=$P(PSNUM,";",2),ORDNUM=$P(PSNUM,";")
 ... I PACK="I" D INPDISP
 ... I PACK="O" D OPTDISP
 ... I PACK="R" D RDIDISP
 ;DSS/MKN - BEGIN MODS - Add Text Only and NewCrop Med orders
 I $G(^%ZOSF("ZVX"))["VX" D VFDNC
 ;DSS/MKN - END MODS
 Q
FOOTER D CKP^GMTSUP Q:$D(GMTSQIT)
 N BLINE
 S BLINE=$$REPEAT^XLFSTR("-",IOM)
 W !,BLINE,!,"Other medications previously dispensed in the last year:",!
 D CKP^GMTSUP Q:$D(GMTSQIT) 
 N DRUGNM,ORDER,PSNUM
 N PACK,PACKREF,SIGLINE
 S DRUGNM="" F  S DRUGNM=$O(SAVE(DRUGNM)) Q:DRUGNM']""  D  ;
 . S ORDER="" F  S ORDER=$O(SAVE(DRUGNM,ORDER)) Q:ORDER']""  D  ;
 .. S PSNUM="" F  S PSNUM=$O(SAVE(DRUGNM,ORDER,PSNUM)) Q:PSNUM']""  D  ;
 ... S PACK=$P(PSNUM,";",2)
 ... I PACK="O" D OPTFOOT
 Q
ADDREM ;6-21-07 ADD ACTIVE MEDS VIA REMOTE DATA INTEROPERABILITY
 N PSOQRDI,PSOQMED,PSOQSTAT,PSOQRNAM,PSOQRNUM,PSOQDOWN
 Q:'$$HAVEHDR^ORRDI1
 D  Q:$G(PSOQDOWN)
 . I $D(^XTMP("ORRDI","OUTAGE INFO","DOWN")) H $$GET^XPAR("ALL","ORRDI PING FREQ")/2
 . I $D(^XTMP("ORRDI","OUTAGE INFO","DOWN")) S PSOQDOWN=1 D
 .. D CKP^GMTSUP Q:$D(GMTSQIT)
 .. W !,"WARNING: Connection to Remote Data Currently Down",!
 .. D CKP^GMTSUP Q:$D(GMTSQIT)
 D  ;18-MAR-08 TO ALLOW HDR/RDI PROCESS TO USE IO VARIABLE
 . D SAVDEV^%ZISUTL("PSOQHFS")
 . S PSOQRDI=$$GET^ORRDI1(DFN,"PSOO")
 . D USE^%ZISUTL("PSOQHFS")
 . D RMDEV^%ZISUTL("PSOQHFS")
 I PSOQRDI=-1 D
 . D CKP^GMTSUP Q:$D(GMTSQIT) 
 . W !,"WARNING: Connection to Remote Data Not Available",!
 . D CKP^GMTSUP Q:$D(GMTSQIT)
 Q:'$D(^XTMP("ORRDI","PSOO",DFN))
 S PSOQMED=0 F  S PSOQMED=$O(^XTMP("ORRDI","PSOO",DFN,PSOQMED)) Q:'+PSOQMED  D
 . S PSOQSTAT=$G(^XTMP("ORRDI","PSOO",DFN,PSOQMED,5,0))
 . Q:PSOQSTAT']""  ;8-3-07 TO CATCH INCOMPLETE RECORDS
 . Q:"ACTIVE^SUSPENDED"'[PSOQSTAT
 . S PSOQRNAM=$G(^XTMP("ORRDI","PSOO",DFN,PSOQMED,2,0),"Unknown Drug")
 . S PSOQRNUM=$G(^XTMP("ORRDI","PSOO",DFN,PSOQMED,4,0))
 . Q:PSOQRNAM']""!(PSOQRNUM']"")
 . S ALPHA(PSOQRNAM,PSOQRNUM,PSOQMED_"X;R")=""
 Q
OPTFOOT N PSOQLRD,PSOQYEAR
 S PACKREF=+$G(^OR(100,ORDER,4))
 S X1=DT,X2=-365 D C^%DTC S PSOQYEAR=X
 S PSOQLRD=$$LRDFUNC^PSOQ0076(PACKREF)
 D CKP^GMTSUP Q:$D(GMTSQIT)
 Q:PSOQLRD<PSOQYEAR
 Q:$P(PSNUM,";")["N"
 W !,"OPT "_DRUGNM_" ("_$$GET1^DIQ(52,+PACKREF,100,"E")_"/"_$$DAYSSUPP^PSOQ0076(PACKREF)_" Days Supply Last Released: "_$$FMTE^XLFDT(PSOQLRD,"2D")_")"  D CKP^GMTSUP Q:$D(GMTSQIT)
 S SIGLINE=0 F  S SIGLINE=$O(^PSRX(PACKREF,"SIG1",SIGLINE)) Q:'+SIGLINE  D  ;
 . W !?5,$G(^PSRX(PACKREF,"SIG1",SIGLINE,0)) D CKP^GMTSUP Q:$D(GMTSQIT)
 W ! D CKP^GMTSUP Q:$D(GMTSQIT)
 Q
INPDISP D CKP^GMTSUP Q:$D(GMTSQIT) 
 W !,"INP "_DRUGNM D CKP^GMTSUP Q:$D(GMTSQIT)
 S LASTACT=$O(^OR(100,+ORDER,8,":"),-1)
 S OTLINE=1 F  S OTLINE=$O(^OR(100,+ORDER,8,LASTACT,.1,OTLINE)) Q:'+OTLINE  D  ;
 . D WRAPTEXT^PSOQUTIL($$LSIG^PSOQUTIL($G(^OR(100,+ORDER,8,LASTACT,.1,OTLINE,0))),60,5) D CKP^GMTSUP Q:$D(GMTSQIT)
 . W !?5,$$BCMALG^PSJUTL2(DFN,ORDNUM) D CKP^GMTSUP Q:$D(GMTSQIT)
 W ! D CKP^GMTSUP Q:$D(GMTSQIT)
 Q
OPTDISP N PSOQEXP,PSOQREF,PSOQSTA
 D CKP^GMTSUP Q:$D(GMTSQIT)
 S PACKREF=+$G(^OR(100,ORDER,4))
 S PSOQLRD=$$LRDFUNC^PSOQ0076(PACKREF)
 S PSOQEXP=$$EXPDATE^PSOQ0076(PACKREF)
 S PSOQREF=$$REFILLS^PSOQ0076(PACKREF)
 I $P(PSNUM,";")["N" G NVADISP
 D  ;
 . N C,Y
 . S Y=$G(^PSRX(PACKREF,"STA"))
 . S C=$P(^DD(52,100,0),U,2)
 . D Y^DIQ
 . S PSOQSTA=Y
 W !,"OPT "_DRUGNM_" (Status = "_PSOQSTA_")"
 S SIGLINE=0 F  S SIGLINE=$O(^PSRX(PACKREF,"SIG1",SIGLINE)) Q:'+SIGLINE  D  ;
 . W !?5,$G(^PSRX(PACKREF,"SIG1",SIGLINE,0)) D CKP^GMTSUP Q:$D(GMTSQIT)
 W !?10,"Last Released: "_$$FMTE^XLFDT(PSOQLRD,"2D"),?55,"Days Supply: "_$$DAYSSUPP^PSOQ0076(PACKREF) D CKP^GMTSUP Q:$D(GMTSQIT)
 W !?10,"Rx Expiration Date: ",$$FMTE^XLFDT(PSOQEXP,"2D"),?55,"Refills Remaining: ",PSOQREF D CKP^GMTSUP Q:$D(GMTSQIT)
 W ! D CKP^GMTSUP Q:$D(GMTSQIT)
 Q
RDIDISP D CKP^GMTSUP Q:$D(GMTSQIT)
 W !,"Remote "_DRUGNM D CKP^GMTSUP Q:$D(GMTSQIT)
 N PSOQSIG,PSOQSTAT
 S PSOQSIG=$G(^XTMP("ORRDI","PSOO",DFN,+ORDNUM,14,0))
 D WRAPTEXT^PSOQUTIL(PSOQSIG,65,5)
 D CKP^GMTSUP Q:$D(GMTSQIT)
 S PSOQSTAT=$G(^XTMP("ORRDI","PSOO",DFN,+ORDNUM,5,0))
 S PSOQSTAT=$S(PSOQSTAT["ACTIVE":"Active",PSOQSTAT["SUSPENDED":"Active/Suspended",1:"Unknown")
 W !?10,"Last Filled: "_$G(^XTMP("ORRDI","PSOO",DFN,+ORDNUM,9,0))_" ("_PSOQSTAT_" at "_$G(^XTMP("ORRDI","PSOO",DFN,+ORDNUM,1,0))_") "
 W:$X>54 ! ;NEW LINE IF THE STATUS+STATION IS TOO LONG
 W ?55,"Days Supply: "_$P($P($G(^XTMP("ORRDI","PSOO",DFN,+ORDNUM,6,0)),";",2),"D",2)
 D CKP^GMTSUP Q:$D(GMTSQIT)
 W !?10,"Rx Expiration Date: ",$G(^XTMP("ORRDI","PSOO",DFN,+ORDNUM,7,0)),?55,"Refills Remaining: ",$G(^XTMP("ORRDI","PSOO",DFN,+ORDNUM,10,0))
 D CKP^GMTSUP Q:$D(GMTSQIT)
 W ! D CKP^GMTSUP Q:$D(GMTSQIT)
 Q
NVADISP D CKP^GMTSUP Q:$D(GMTSQIT)
 ;DSS/SMP - Begin Mods
 I $G(^%ZOSF("ZVX"))["VX" D WRAPTEXT^PSOQUTIL("Meds/OTC from Elsewhere "_DRUGNM,73) D CKP^GMTSUP Q:$D(GMTSQIT)
 I $G(^%ZOSF("ZVX"))'["VX" W !,"Non VA "_DRUGNM D CKP^GMTSUP Q:$D(GMTSQIT)
 ;DSSM/SMP - End Mods
 S LASTACT=$O(^OR(100,ORDER,8,":"),-1)
 S OTLINE=1 F  S OTLINE=$O(^OR(100,ORDER,8,LASTACT,.1,OTLINE)) Q:'+OTLINE  D  ;
 .D WRAPTEXT^PSOQUTIL($G(^OR(100,ORDER,8,LASTACT,.1,OTLINE,0)),65,5) D CKP^GMTSUP Q:$D(GMTSQIT)
 W ! D CKP^GMTSUP Q:$D(GMTSQIT)
 Q
 ;DSS/MKN - BEGIN MODS - Print Text Only and NewCrop meds
VFDNC ;
 N VFDARR,VFDC,VFDDRUG,VFDI,VFDJ,VFDN,VFDORD,VFDORDN,VFDPREF,VFDREL,VFDSTA,VFDSORT,VFDSTOP,VFDTX,VFDTXT,VFDW,VFDX,VFDX2,VFDY
 D AGET^ORWORR(.VFDARR,DFN,"1^0",1,0,0,,0)
 S VFDN=+$G(@VFDARR@(.1)) F VFDI=1:1:VFDN S VFDX=@VFDARR@(VFDI),VFDORDN=+VFDX D
 . K VFDORD D GETS^DIQ(100,VFDORDN_",","2;5;7;.8*;22;33","IE","VFDORD","ERR")
 . I $G(VFDORD(100,VFDORDN_",",2,"E"))="OR GXTEXT WORD PROCESSING ORDER" D
 . . S VFDX="",VFDJ=0 F  S VFDJ=$O(VFDORD(100.008,"1,"_VFDORDN_",",.1,VFDJ)) Q:'VFDJ  D
 . . . S VFDX=VFDX_VFDORD(100.008,"1,"_VFDORDN_",",.1,VFDJ)
 . . S VFDDRUG=$P(VFDX,"~",3) I VFDDRUG'="" D
 . . . S VFDDRUG=$$VFDNOSP(VFDDRUG)
 . . . S VFDSTA=$G(VFDORD(100,VFDORDN_",",5,"E")),VFDREL=$G(VFDORD(100.008,"1,"_VFDORDN_",",16,"I"))
 . . . S VFDREL=$$FMTE^XLFDT(VFDREL,"2D"),VFDPREF=$G(VFDORD(100,VFDORDN_",",33,"E"))
 . . . S VFDSTOP=$G(VFDORD(100,VFDORDN_",",22,"I")),VFDSTOP=$$FMTE^XLFDT(VFDSTOP,"2D")
 . . . S VFDSORT(VFDDRUG)=VFDSTA_U_VFDREL_U_$P($P(VFDX,"~",9),U,2)_U_VFDSTOP_U_$P($P(VFDX,"~",8),U,2)_U_VFDPREF
 S VFDDRUG="" F  S VFDDRUG=$O(VFDSORT(VFDDRUG)) Q:VFDDRUG=""  S VFDX=VFDSORT(VFDDRUG) D
 . D CKP^GMTSUP Q:$D(GMTSQIT)
 . ;W !,$S($P($P(VFDX,U,6),"~",2)'="":"NC ",1:"TXT")_" "_VFDDRUG_" (Status = "_$P(VFDX,U,1)_")"
 . W !,$S($P($P(VFDX,U,6),"~",2)'="":"eRx",1:"TXT")_" "
 . S VFDX2=VFDDRUG_" (Status = "_$P(VFDX,U,1)_")",VFDW=74
 . D VFDWRAP(VFDX2) F VFDI=1:1 Q:'$D(VFDTX(VFDI))  W:VFDI>1 !,"    " W VFDTX(VFDI) D CKP^GMTSUP Q:$D(GMTSQIT)
 . W !?10,"Last Released: "_$$FMTE^XLFDT($P(VFDX,U,2),"2D"),?55,"Days Supply: "_$P(VFDX,U,3) D CKP^GMTSUP Q:$D(GMTSQIT)
 . W !?10,"Rx Expiration Date: ",$P(VFDX,U,4),?55,"Refills Remaining: ",$P(VFDX,U,5) D CKP^GMTSUP Q:$D(GMTSQIT)
 . W ! D CKP^GMTSUP Q:$D(GMTSQIT)
 Q
VFDWRAP(VFDX) ;Split text in variable X into an array where each line length is determined by VFDW
 N VFDI,VFDY,VFDZ K VFDTX S VFDTX=0,VFDY=$L(VFDX) S:VFDY VFDY=VFDY+1 ;allow for space
 I VFDY+$L(VFDX)'>VFDW S VFDTX(1)=$$VFDNOSP(VFDX) Q
 F VFDI=1:1:$L(VFDX," ") S VFDZ=$P(VFDX," ",VFDI) D:(VFDY+$L(VFDZ))>VFDW  S VFDTX(VFDTX)=$G(VFDTX(VFDTX))_$S(VFDY:" ",1:"")_VFDZ,VFDY=$L(VFDTX(VFDTX)) S:VFDY VFDY=VFDY+1
 . I $L(VFDZ)>VFDW F  S VFDTX(VFDTX)=$G(VFDTX(VFDTX))_$S(VFDY:" ",1:"")_$E(VFDZ,1,VFDW-VFDY),VFDZ=$E(VFDZ,VFDW-VFDY+1,999) Q:$L(VFDZ)'>VFDW  S VFDTX=VFDTX+1,VFDY=0
 . S VFDTX=VFDTX+1,VFDY=0
 S VFDI="" F  S VFDI=$O(VFDTX(VFDI)) Q:VFDI=""  S VFDTX(VFDI)=$$VFDNOSP(VFDTX(VFDI))
 Q
 ;
VFDNOSP(VFDX) ;Remove leading and trailing spaces
 N VFDI
 F VFDI=1:1:$L(VFDX) Q:$E(VFDX,VFDI)'=" "
 S VFDX=$E(VFDX,VFDI,$L(VFDX))
 F VFDI=$L(VFDX):-1:1 Q:$E(VFDX,VFDI)'=" "
 S VFDX=$E(VFDX,1,VFDI)
 Q VFDX
 ;
