SDEC57A ;ALB/SAT - VISTA SCHEDULING RPCS ;JAN 15, 2016
 ;;5.3;Scheduling;**627**;Aug 13, 1993;Build 249
 ;
 Q
 ;
 ;build access block array SDBLKS from pattern SDPAT
GETBLKS(SDBLKS,SDPAT,SDAY,SDCLS,SDLEN,SDSI,SDCL) ;
 ;INPUT:
 ;  SDPAT - Pattern from CURRENT AVAILABILITY field of PATTERN multiple in file 44
 ;  SDAY  - date (no time) in FM format
 ;  SDCLS - hour clinic display begins from field 1914 in file 44
 ;  SDLEN - length of app't from field 1912 in file 44
 ;  SDSI  - display increments per hour
 ;  SDCL  - clinic IEN
 ;RETURN:
 ; .SDBLKS - array of access block data to be stored in SDEC ACCESS BLOCK file
 ;           SDBLKS(<count>)=<start time> ^ <end time> ^ <slots> ^ <access type>
 N DTARRAY
 N SDATAV,SDATCA,SDATUN,SDF,SDI,SDSIM,SDSTPAT
 S SDF=0
 ;get SDEC ACCESS TYPEs
 S SDATAV=$O(^SDEC(409.823,"B","AVAILABLE",0))
 S SDATCA=$O(^SDEC(409.823,"B","CANCELED",0))
 S SDATUN=$O(^SDEC(409.823,"B","UNAVAILABLE",0))
 ;SDSIM - calculated using DISPLAY INCREMENTS PER HOUR field from file 44
 S SDSIM=$S(SDSI="":4,SDSI<3:4,SDSI:SDSI,1:4)
 S SDSTPAT=$G(^SC(SDCL,"ST",SDAY,1)) I SDSTPAT["CANCELLED" S SDF=1,SDSTPAT=$G(^SC(SDCL,"ST",SDAY,"CAN"))   ;get PATTERN from file 44
 S SDSTPAT=$E(SDSTPAT,SDSIM+SDSIM,90)
 D:SDSTPAT'="" ARRAY^SDECUTL2(.DTARRAY,SDSTPAT,SDAY,SDLEN,SDCLS,SDSI) ;convert pattern to array
 S SDPAT=SDSTPAT
 K SDBLKS
 ;1 2 3 4 OR 6
 D @SDSI
 Q
1  ;1 increments per hour (60 min)
 N AU,BCNT,BMIN,BSLOT,BSTART,BTIME,BSTOP,CNT,CNT1,HOUR,HR,HRP,P1,P2,SDCAN,SDTAR,SDI,SLOT,STA
 D A^SDECUT1A(.STA,SDCL,SDAY,SDSI,SDCLS)
 S SDI=0,SLOT=""
 F CNT1=2:2 S SLOT=$E(SDPAT,CNT1) Q:$$VAL(SLOT)  Q:CNT1>$L(SDPAT)  ;find 1st slot ;might not be on the hour
 I CNT1>6 D
 .S BSTART=SDAY_"."_$S($L(SDCLS)=1:"0"_SDCLS,1:SDCLS)
 .S BTIME=SDCLS+(CNT1\8)
 .S HR=$S($L(BTIME)=1:"0"_BTIME,1:BTIME)
 .I '$D(STA(HR)) D STA
 .S BTIME=$S($L(BTIME)=1:"0"_BTIME,1:BTIME)_$S((CNT1#8)=4:$P(STA(HR,4),U,2),(CNT1#8)=6:$P(STA(HR,6),U,2),(CNT1#8)=0:$P(STA(HR,0),U,2),1:$P(STA(HR,2),U,2))
 .S BSTOP=SDAY_"."_BTIME
 .S SDI=SDI+1 S SDBLKS(SDI)=BSTART_U_BSTOP_U_0_U_SDATUN
 S (BSLOT,BSTART)=""
 S HOUR=(SDCLS+(CNT1\8))-1
 F CNT=CNT1:8 D  Q:SLOT=""
 .S HOUR=HOUR+1   ;I (CNT#8)=2 S HOUR=HOUR+1
 .S HR=$S($L(HOUR)=1:"0"_HOUR,1:HOUR)
 .I '$D(STA(HR)) D STA
 .S:BSTART="" BSTART=SDAY_"."_HR_$S((CNT#8)=4:$P(STA(HR,4),U,2),(CNT#8)=6:$P(STA(HR,6),U,2),(CNT#8)=0:$P(STA(HR,0),U,2),1:$P(STA(HR,2),U,2))
 .S SDCAN=$G(DTARRAY(SDAY,HR))="X"
 .S SLOT=$S(SDCAN:"X",1:$E(SDPAT,CNT))
 .S SLOT=$S(SDCAN:"X",SLOT="":SLOT,$$VAL(SLOT):SLOT,1:" ")
 .S:BSLOT="" BSLOT=$S(SLOT="X":SLOT,$$VAL(SLOT):SLOT,1:" ")
 .I 1 D
 ..I BSLOT=" ",SLOT="",$E($P(BSTART,".",2),1,2)<18 Q
 ..S BMIN=$S((CNT#8)=4:$P(STA(HR,4),U,2),(CNT#8)=6:$P(STA(HR,6),U,2),(CNT#8)=0:$P(STA(HR,0),U,2),1:$P(STA(HR,2),U,2))
 ..S BTIME=$S((BMIN="")&((HOUR#10)=0):$E(HOUR),1:$S($L(HOUR)=1:"0"_HOUR,1:HOUR))_$S(BMIN'="":BMIN,1:"")
 ..S BSTOP=SDAY_"."_BTIME
 ..I $E($P(BSTOP,".",2),1,2)>23 S BSTOP=$P(BSTOP,".",1)_".2359"
 ..I BSTART'=BSTOP D
 ...S SDI=SDI+1 S SDBLKS(SDI)=BSTART_U_BSTOP_U_BSLOT_U_$S(BSLOT="X":SDATCA,BSLOT=" ":SDATUN,1:SDATAV)
 ..S BSLOT=$S(SLOT="X":"X",$$VAL(SLOT):SLOT,1:" ")
 ..S BSTART=BSTOP   ;SDAY_"."_$S($L(HOUR)=1:"0"_HOUR,1:HOUR)   ;_$S((CNT#8)=4:15,(CNT#8)=6:3,(CNT#8)=0:45,1:"")
 .S BCNT=CNT
 I $E($P(BSTART,".",2),1,2)<18 S SDI=SDI+1 S SDBLKS(SDI)=BSTART_U_SDAY_"."_18_U_0_U_SDATUN
 Q
2  ;2 increments per hour (30 min)
 N AU,BCNT,BSLOT,BSTART,BSTOP,BTIME,CNT,CNT1,HOUR,HR,HRP,P1,P2,SDCAN,SDI,SDTAR,SLOT,STA
 S (BSLOT,BSTART,SLOT,STA)=""
 S SDI=0
 S HOUR=SDCLS-1
 ;find 1st slot-might not be on the hour
 F CNT1=2:2 S:(CNT1#8)=2 HOUR=HOUR+1 S SLOT=$E(SDPAT,CNT1) Q:$$VAL(SLOT)  Q:CNT1>$L(SDPAT)
 Q:CNT1>$L(SDPAT)
 ;get array of start times
 D A^SDECUT1A(.STA,SDCL,SDAY,SDSI,SDCLS)
 I CNT1>2 D
 .S BSTART=SDAY_"."_$S($L(SDCLS)=1:"0"_SDCLS,1:SDCLS)
 .S HR=$S($L(HOUR)=1:"0"_HOUR,1:HOUR)
 .I '$D(STA(HR)) D STA
 .S BSTOP=SDAY_"."_HR_$S((CNT1#8)=4:$P(STA(HR,4),U,2),(CNT1#8)=6:$P(STA(HR,6),U,2),(CNT1#8)=0:$P(STA(HR,0),U,2),1:$P(STA(HR,2),U,2))
 .D MAKE(.SDBLKS,.SDI,BSTART,BSTOP,0)
 S BSTART=""
 I ((CNT1#8)=2)!((CNT1#8)=4) S HOUR=HOUR-1
 F CNT=CNT1:4 D  Q:SLOT=""
 .I ((CNT#8)=2)!((CNT#8)=4) S HOUR=HOUR+1
 .S HR=$S($L(HOUR)=1:"0"_HOUR,1:HOUR)
 .I '$D(STA(HR)) D STA
 .S:BSTART="" BSTART=SDAY_"."_HR_$S((CNT#8)=4:$P(STA(HR,4),U,2),(CNT#8)=6:$P(STA(HR,6),U,2),(CNT#8)=0:$P(STA(HR,0),U,2),1:$P(STA(HR,2),U,2))
 .S SDCAN=$G(DTARRAY(SDAY,HR_$S((CNT#8)=4:$P(STA(HR,4),U,1),(CNT#8)=6:$P(STA(HR,6),U,1),(CNT#8)=0:$P(STA(HR,0),U,1),1:$P(STA(HR,2),U,1))))="X"
 .S SLOT=$S(SDCAN:"X",1:$E(SDPAT,CNT))
 .S SLOT=$S(SDCAN:"X",SLOT="":SLOT,$$VAL(SLOT):SLOT,1:" ")
 .S:BSLOT="" BSLOT=$S(SLOT="X":SLOT,$$VAL(SLOT):SLOT,1:" ")
 .I 1 D
 ..I BSLOT=" ",SLOT="",$E($P(BSTART,".",2),1,2)<18 Q
 ..S BTIME=$S(((CNT#8)=2)&((HOUR#10)=0):$E(HOUR),1:$S($L(HOUR)=1:"0"_HOUR,1:HOUR)_$S((CNT#8)=4:$P(STA(HR,4),U,2),(CNT#8)=6:$P(STA(HR,6),U,2),(CNT#8)=0:$P(STA(HR,0),U,2),1:$P(STA(HR,2),U,2)))
 ..S BSTOP=SDAY_"."_BTIME
 ..I $E($P(BSTOP,".",2),1,2)>23 S BSTOP=$P(BSTOP,".",1)_".2359"
 ..I BSTART'=BSTOP D
 ...S SDI=SDI+1 S SDBLKS(SDI)=BSTART_U_BSTOP_U_BSLOT_U_$S(BSLOT="X":SDATCA,BSLOT=" ":SDATUN,1:SDATAV)
 ..S BSLOT=$S(SLOT="X":"X",$$VAL(SLOT):SLOT,1:" ")
 ..S BSTART=BSTOP   ;SDAY_"."_$S($L(HOUR)=1:"0"_HOUR,1:HOUR)_$S((CNT#8)=6:3,1:"")
 .S BCNT=CNT
 I $E($P(BSTART,".",2),1,2)<18 S SDI=SDI+1 S SDBLKS(SDI)=BSTART_U_SDAY_"."_18_U_U_SDATUN
 Q
3  ;3 increments per hour (20 min)
 N AU,BCNT,BSLOT,BSTART,BSTOP,BTIME,CNT,CNT1,HOUR,HR,HRP,P1,P2,SDCAN,SDI,SDTAR,SLOT,STA
 S (BSLOT,BSTART,SLOT)=""
 S SDI=0
 S HOUR=SDCLS-1
 F CNT1=2:2 S:(CNT1#6)=2 HOUR=HOUR+1 S SLOT=$E(SDPAT,CNT1) Q:$$VAL(SLOT)  Q:CNT1>$L(SDPAT)   ;find 1st slot ;might not be on the hour
 Q:CNT1>$L(SDPAT)
 D A^SDECUT1A(.STA,SDCL,SDAY,SDSI,SDCLS)
 I CNT1>2 D
 .S BSTART=SDAY_"."_$S($L(SDCLS)=1:"0"_SDCLS,1:SDCLS)
 .S HR=$S($L(HOUR)=1:"0"_HOUR,1:HOUR)
 .I '$D(STA(HR)) D STA
 .S BSTOP=SDAY_"."_HR_$S((CNT1#6)=4:$P(STA(HR,4),U,2),(CNT1#6)=0:$P(STA(HR,0),U,2),1:$P(STA(HR,2),U,2))
 .D MAKE(.SDBLKS,.SDI,BSTART,BSTOP,0)
 S BSTART=""
 I ((CNT1#6)=2) S HOUR=HOUR-1
 F CNT=CNT1:2 D  Q:SLOT=""
 .I (CNT#6)=2 S HOUR=HOUR+1
 .S HR=$S($L(HOUR)=1:"0"_HOUR,1:HOUR)
 .I '$D(STA(HR)) D STA
 .S:BSTART="" BSTART=SDAY_"."_HR_$S((CNT#6)=4:$P(STA(HR,4),U,2),(CNT#6)=0:$P(STA(HR,0),U,2),1:$P(STA(HR,2),U,2))
 .S SDCAN=$G(DTARRAY(SDAY,HR_$S((CNT#6)=4:$P(STA(HR,4),U,1),(CNT#6)=0:$P(STA(HR,0),U,1),1:$P(STA(HR,2),U,1))))="X"
 .S SLOT=$S(SDCAN:"X",1:$E(SDPAT,CNT))
 .S SLOT=$S(SDCAN:"X",SLOT="":SLOT,$$VAL(SLOT):SLOT,1:" ")
 .S:BSLOT="" BSLOT=$S(SLOT="X":SLOT,$$VAL(SLOT):SLOT,1:" ")
 .I 1 D
 ..I BSLOT=" ",SLOT="",$E($P(BSTART,".",2),1,2)<18 Q
 ..S BTIME=$S(((CNT#6)=2)&((HOUR#10)=0):$E(HOUR),1:$S($L(HOUR)=1:"0"_HOUR,1:HOUR)_$S((CNT#6)=4:$P(STA(HR,4),U,2),(CNT#6)=0:$P(STA(HR,0),U,2),1:$P(STA(HR,2),U,2)))
 ..S BSTOP=SDAY_"."_BTIME
 ..I $E($P(BSTOP,".",2),1,2)>23 S BSTOP=$P(BSTOP,".",1)_".2359"
 ..I BSTART'=BSTOP D
 ...S SDI=SDI+1 S SDBLKS(SDI)=BSTART_U_BSTOP_U_BSLOT_U_$S(BSLOT="X":SDATCA,BSLOT=" ":SDATUN,1:SDATAV)
 ..S BSLOT=$S(SLOT="X":"X",$$VAL(SLOT):SLOT,1:" ")
 ..S BSTART=BSTOP   ;SDAY_"."_$S($L(HOUR)=1:"0"_HOUR,1:HOUR)_$S((CNT#6)=4:2,(CNT#6)=0:4,1:"")
 .S BCNT=CNT
 I $E($P(BSTART,".",2),1,2)<18 S SDI=SDI+1 S SDBLKS(SDI)=BSTART_U_SDAY_"."_18_U_U_SDATUN
 Q
4  ;4 increments per hour (15 min)
 N AU,BCNT,BSLOT,BSTART,BSTOP,BTIME,CNT,CNT1,HOUR,HR,HRP,P1,P2,SDCAN,SDI,SDTAR,SLOT,STA
 S (BSLOT,BSTART,SLOT,STA)=""
 S SDI=0
 D A^SDECUT1A(.STA,SDCL,SDAY,SDSI,SDCLS)
 S HOUR=SDCLS-1
 F CNT=2:2 D  Q:SLOT=""
 .I (CNT#8)=2 S HOUR=HOUR+1
 .S HR=$S($L(HOUR)=1:"0"_HOUR,1:HOUR)
 .I '$D(STA(HR)) D STA
 .S:BSTART="" BSTART=SDAY_"."_HR_$S((CNT#8)=4:$P(STA(HR,4),U,2),(CNT#8)=6:$P(STA(HR,6),U,2),(CNT#8)=0:$P(STA(HR,0),U,2),1:$P(STA(HR,2),U,2))
 .S SDCAN=$G(DTARRAY(SDAY,HR_$S((CNT#8)=4:$P(STA(HR,4),U,1),(CNT#8)=6:$P(STA(HR,6),U,1),(CNT#8)=0:$P(STA(HR,0),U,1),1:$P(STA(HR,2),U,1))))="X"
 .S SLOT=$S(SDCAN:"X",1:$E(SDPAT,CNT))
 .S SLOT=$S(SDCAN:"X",SLOT="":SLOT,$$VAL(SLOT):SLOT,1:" ")
 .S:BSLOT="" BSLOT=$S(SLOT="X":SLOT,$$VAL(SLOT):SLOT,1:" ")
 .I 1 D
 ..I BSLOT=" ",SLOT="",$E($P(BSTART,".",2),1,2)<18 Q
 ..S BTIME=$S(((CNT#8)=2)&((HOUR#10)=0):$E(HOUR),1:$S($L(HOUR)=1:"0"_HOUR,1:HOUR)_$S((CNT#8)=4:$P(STA(HR,4),U,2),(CNT#8)=6:$P(STA(HR,6),U,2),(CNT#8)=0:$P(STA(HR,0),U,2),1:$P(STA(HR,2),U,2)))
 ..S BSTOP=SDAY_"."_BTIME
 ..I $E($P(BSTOP,".",2),1,2)>23 S BSTOP=$P(BSTOP,".",1)_".2359"
 ..I BSTART'=BSTOP D
 ...S SDI=SDI+1 S SDBLKS(SDI)=BSTART_U_BSTOP_U_BSLOT_U_$S(BSLOT="X":SDATCA,BSLOT=" ":SDATUN,1:SDATAV)
 ..S BSLOT=$S(SLOT="X":"X",$$VAL(SLOT):SLOT,1:" ")
 ..S BSTART=BSTOP   ;SDAY_"."_$S($L(HOUR)=1:"0"_HOUR,1:HOUR)_$S((CNT#8)=4:15,(CNT#8)=6:3,(CNT#8)=0:45,1:"")
 .S BCNT=CNT
 I $E($P(BSTART,".",2),1,2)<18 S SDI=SDI+1 S SDBLKS(SDI)=BSTART_U_SDAY_"."_18_U_U_SDATUN
 Q
6  ;6 increments per hour (10 min)
 N AU,BCNT,BSLOT,BSTART,BSTOP,BTIME,CNT,CNT1,HOUR,HR,HRP,P1,P2,SDCAN,SDI,SDTAR,SLOT,STA
 S (BSLOT,BSTART,SLOT)=""
 S SDI=0
 D A^SDECUT1A(.STA,SDCL,SDAY,SDSI,SDCLS)
 S HOUR=SDCLS-1
 F CNT=2:2 D  Q:SLOT=""
 .I (CNT#12)=2 S HOUR=HOUR+1
 .S HR=$S($L(HOUR)=1:"0"_HOUR,1:HOUR)
 .I '$D(STA(HR)) D STA
 .S:BSTART="" BSTART=SDAY_"."_HR_$S((CNT#12)=4:$P(STA(HR,4),U,2),(CNT#12)=6:$P(STA(HR,6),U,2),(CNT#12)=8:$P(STA(HR,8),U,2),(CNT#12)=10:$P(STA(HR,10),U,2),(CNT#12)=0:$P(STA(HR,0),U,2),1:$P(STA(HR,2),U,2))
 .S SDCAN=$G(DTARRAY(SDAY,$S($L(HOUR)=1:"0"_HOUR,1:HOUR)_$S((CNT#12)=4:$P(STA(HR,4),U,1),(CNT#12)=6:$P(STA(HR,6),U,1),(CNT#12)=8:$P(STA(HR,8),U,1),(CNT#12)=10:$P(STA(HR,10),U,1),(CNT#12)=0:$P(STA(HR,0),U,1),1:$P(STA(HR,2),U,1))))="X"
 .S SLOT=$S(SDCAN:"X",1:$E(SDPAT,CNT))
 .S SLOT=$S(SDCAN:"X",SLOT="":SLOT,$$VAL(SLOT):SLOT,1:" ")
 .S:BSLOT="" BSLOT=$S(SLOT="X":SLOT,$$VAL(SLOT):SLOT,1:" ")
 .I 1 D
 ..I BSLOT=" ",SLOT="",$E($P(BSTART,".",2),1,2)<18 Q
 ..S HR=$S($L(HOUR)=1:"0"_HOUR,1:HOUR)
 ..S BTIME=HR_$S((CNT#12)=4:$P(STA(HR,4),U,2),(CNT#12)=6:$P(STA(HR,6),U,2),(CNT#12)=8:$P(STA(HR,8),U,2),(CNT#12)=10:$P(STA(HR,10),U,2),(CNT#12)=0:$P(STA(HR,0),U,2),1:$P(STA(HR,2),U,2))
 ..S BSTOP=SDAY_"."_BTIME
 ..I $E($P(BSTOP,".",2),1,2)>23 S BSTOP=$P(BSTOP,".",1)_".2359"
 ..I BSTART'=BSTOP D
 ...S SDI=SDI+1 S SDBLKS(SDI)=BSTART_U_BSTOP_U_BSLOT_U_$S(BSLOT="X":SDATCA,BSLOT=" ":SDATUN,1:SDATAV)
 ..S BSLOT=$S(SLOT="X":"X",$$VAL(SLOT):SLOT,1:" ")
 ..S BSTART=BSTOP   ;SDAY_"."_$S($L(HOUR)=1:"0"_HOUR,1:HOUR)_$S((CNT#12)=4:1,(CNT#12)=6:2,(CNT#12)=8:3,(CNT#12)=10:4,(CNT#12)=0:5,1:"")
 .S BCNT=CNT
 I $E($P(BSTART,".",2),1,2)<18 S SDI=SDI+1 S SDBLKS(SDI)=BSTART_U_SDAY_"."_18_U_U_SDATUN
 Q
STA ;
 N HRP
 S HRP=HR-1 S HRP=$S($L(HRP)=1:"0"_HRP,1:HRP)
 I $D(STA(HRP)) D
 .S STA(HR,4)=STA(HRP,4)
 .S:SDSI'=3 STA(HR,6)=STA(HRP,6)
 .S:SDSI=6 STA(HR,8)=STA(HRP,8)
 .S:SDSI=6 STA(HR,10)=STA(HRP,10)
 .S STA(HR,0)=STA(HRP,0)
 .S STA(HR,2)=STA(HRP,2)
 E  X "D B"_SDSI_"^SDECUT1A(.STA,"""_HR_""",0)"
 Q
 ;
MAKE(SDBLKS,SDI,START,STOP,SLOT)  ;make block
 N SDATAV,SDATUN
 S SDATAV=$O(^SDEC(409.823,"B","AVAILABLE",0))
 S SDATUN=$O(^SDEC(409.823,"B","UNAVAILABLE",0))
 S SDI=SDI+1 S SDBLKS(SDI)=START_U_STOP_U_SLOT_U_$S($$VAL(SLOT):SDATAV,1:SDATUN)
 Q
 ;0-9,j-z for 0 to 26 available appts, A-W for overbooks 1-23,
 ;'*$!@#?' for overbook outside normal hours, X for cancelled
VAL(SLOT) ;Return 1 if valid available/overbook slots character
 I $L(SLOT)=0 Q 0
 Q "*$!@#0123456789jklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWX"[$E(SLOT,1)
